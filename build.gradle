
plugins {
    id 'java-library'
    id 'application'
    id 'signing'
    id 'maven'
}

ext {
    artifactId = "idpass-lite-java"
    groupId = "io.github.idpass"
    version = '1.0.0'
    description = "IDPass (Java) is a wrapper API of libidpasslite.so"
}

repositories {
    mavenCentral()
    google()
    jcenter()
}

group project.ext.groupId
version = project.ext.version

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
    main {
        resources {
            srcDirs "src/main/resources"
        }
    }
}

jar {
    from {
        (configurations.runtime).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

test {
    useJUnitPlatform()
}

// Dependencies
dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'

    testImplementation 'org.mockito:mockito-core:2.28.2'

    //////////////// idpass //////////////////////
    implementation 'com.google.protobuf:protobuf-java:3.12.2'
    implementation 'com.google.zxing:core:3.4.0'
    implementation 'com.google.zxing:javase:3.4.0'
    implementation 'at.favre.lib:bytes:1.3.0'
    compile files('src/main/java/proto/idpasslite.proto.jar')

    // These jars only needed by TestCases.java for unit testing purposes only
    // will not be a transitive dependency in client apps. It is used to convert
    // PNG to SVG as an additional test case
    testImplementation group: 'org.apache.xmlgraphics', name: 'batik-transcoder', version: '1.13'
    testRuntimeOnly group: 'org.apache.xmlgraphics', name: 'batik-transcoder', version: '1.13'
    testImplementation group: 'com.twelvemonkeys.imageio', name: 'imageio-batik', version: '3.6'
    testRuntimeOnly group: 'com.twelvemonkeys.imageio', name: 'imageio-batik', version: '3.6'
}

signing {
    if (project.hasProperty('signing.keyId') &&
        project.hasProperty('signing.password') &&
        project.hasProperty('signing.secretKeyRingFile')) {
        sign configurations.archives
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

tasks.withType(Test) {
    // a collection to track failedTests
    ext.failedTests = []

    afterTest { descriptor, result ->
        if (result.resultType == TestResult.ResultType.FAILURE) {
            String failedTest = "${descriptor.className}::${descriptor.name}"
            logger.debug("Adding " + failedTest + " to failedTests...")
            failedTests << [failedTest]
        }
    }

    afterSuite { suite, result ->
        if (!suite.parent) { // will match the outermost suite
            // logs each failed test
            if (!failedTests.empty) {
                logger.lifecycle("Failed tests:")
                failedTests.each { failedTest ->
                    logger.lifecycle("${failedTest}")
                }
            }
        }
    }
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'IDPASS LITE'
        packaging 'jar'
        // optionally artifactId can be defined here
        artifactId project.ext.artifactId
        description 'IDPass (Java) is a wrapper API of libidpasslite.so'
        url 'https://github.com/idpass/idpass-lite-java'

        scm {
          connection 'scm:git:git://github.com/idpass/idpass-lite-java.git'
          developerConnection 'scm:git:ssh://github.com:idpass/idpass-lite-java.git'
          url 'https://github.com/idpass/idpass-lite-java/tree/master'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'typelogic'
            name 'Dexter Aparicio'
            email 'dexter@newlogic.com'
          }
        }
      }
    }
  }
}
